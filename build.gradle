plugins {
	id 'fabric-loom' version '1.0-SNAPSHOT'
	id 'maven-publish'
	id 'io.github.juuxel.loom-quiltflower' version '1.6.0'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
	mavenLocal()
	mavenCentral()

	maven { url 'https://maven.wispforest.io' }
	maven { url "https://maven.terraformersmc.com/releases/" }
	maven { url "https://api.modrinth.com/maven"
		content {includeGroup "maven.modrinth"}
	}
	maven { url 'https://ladysnake.jfrog.io/artifactory/mods' }
	maven { url 'https://maven.cafeteria.dev'
		content {includeGroup 'net.adriantodt.fabricmc'}
	}
	maven { url "https://maven.jamieswhiteshirt.com/libs-release"
		content {includeGroup "com.jamieswhiteshirt"}
	}
	maven { url "https://jitpack.io" }
	maven { url "https://maven.shedaniel.me/" }

	maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }

	maven { /* name "Ladysnake Mods" */ url "https://ladysnake.jfrog.io/artifactory/mods"
		content {
			includeGroup 'io.github.ladysnake'
			includeGroupByRegex 'io\\.github\\.onyxstudios.*'
		}
	}

	maven { /*name "KosmX's maven"*/ url 'https://maven.kosmx.dev/' }
	maven { url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/' }
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	//Dependencies
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	include "io.wispforest:owo-sentinel:${project.owo_version}"
	annotationProcessor modImplementation("io.wispforest:owo-lib:${project.owo_version}")

	include modImplementation ("io.github.ladysnake:satin:${satin_version}")

//	include modImplementation("dev.kosmx.player-anim:player-animation-lib-fabric:${project.player_anim}")

	modImplementation ("software.bernie.geckolib:geckolib-fabric-1.19:${project.gecko}")

	//---

	include implementation ('com.github.Dansoftowner:jSystemThemeDetector:3.8')
	include ('io.github.g00fy2:versioncompare:1.4.1')

	include ('de.jangassen:jfa:1.2.0') { exclude group: 'net.java.dev.jna', module: 'jna' /* different jna version */}

	include implementation ('org.apache.commons:commons-collections4:4.4')
	include implementation ('io.github.earcut4j:earcut4j:2.2.2')
	include implementation ('me.xdrop:fuzzywuzzy:1.4.0')

	include implementation ('com.google.code.gson:gson:2.10')
	//


	//Compat only
	modImplementation("com.github.Virtuoel:Pehkui:${pehkui_version}", { exclude group: "net.fabricmc.fabric-api" })

	modImplementation("com.terraformersmc:modmenu:${project.modmenu_version}")

	modCompileOnly("maven.modrinth:additionalentityattributes:1.2.0+1.19.2")
	modCompileOnly("com.github.apace100:apoli:v2.6.0", { exclude group: "com.github.DaFuqs", module: "AdditionalEntityAttributes" })
	modCompileOnly("com.github.apace100:origins-fabric:${project.origin_version}", { exclude group: "com.github.apace100", module: "apoli" })

	/*modImplementation*/modRuntimeOnly("com.github.apace100:origins-fabric:${project.origin_version}")


	modImplementation("dev.emi:trinkets:${trinkets_version}")

	modCompileOnly "dev.onyxstudios.cardinal-components-api:cardinal-components-base:${project.cc_version}"
	modCompileOnly "dev.onyxstudios.cardinal-components-api:cardinal-components-entity:${project.cc_version}"
	//


	//Runtime Only
	modLocalRuntime("maven.modrinth:lazydfu:0.1.3")
	modLocalRuntime("maven.modrinth:ferrite-core:5.0.3-fabric")
	modLocalRuntime("maven.modrinth:smoothboot-fabric:1.19-1.7.1")

	modLocalRuntime("maven.modrinth:carpet:1.4.84")

//	modLocalRuntime "io.wispforest:gadget:${project.gadget_version}"
	//
}

loom {
	accessWidenerPath = file("src/main/resources/personality.accesswidener")

	runs {

		Integer numberOfRuns = 1;

		try {
			numberOfRuns = Integer.valueOf(project.setsOfRunConfigs);
		} catch(NumberFormatException ignored){}

		for(int i = 1; i <= numberOfRuns; i++) {
			it.create("clientMixinDebug_${i}", {
				client()
				runDir "run_${i}"
				name "Minecraft Client ${i} [Mixin Debug]"
				vmArg "-Dfabric.dli.config=${file(".gradle/loom-cache/launch.cfg").toString()}"
				vmArg "-Dfabric.dli.env=client"
				vmArg "-Dfabric.dli.main=net.fabricmc.loader.impl.launch.knot.KnotClient"

				Object runConfig = it

				project.afterEvaluate {
					project.configurations.compileClasspath
							.getAllDependencies()
							.findAll { Dependency test -> test.getName() == "sponge-mixin" }
							.iterator().forEachRemaining {runConfig.vmArg "-javaagent:\"${(project.configurations.compileClasspath.files(it).iterator().next().getPath())}\"" }
				}

//				vmArg "-Dowo.renderdocPath=C:\\Program Files\\RenderDoc\\renderdoc.dll"

				vmArg "-Dlog4j.configurationFile=${file(".gradle/loom-cache/log4j.xml").toString()}"
				vmArg "-Dfabric.log.disableAnsi=false"
				vmArg "-Dmixin.debug.export=true"

				ideConfigGenerated = true
			})
		}

		clientRenderDoc {
			client()
			name "Minecraft Client [RenderDoc]"
//			vmArg "-Dfabric.dli.config=${file(".gradle/loom-cache/launch.cfg").toString()}"
//			vmArg "-Dfabric.dli.env=client"
//			vmArg "-Dfabric.dli.main=net.fabricmc.loader.impl.launch.knot.KnotClient"

			vmArg "-Dowo.renderdocPath=\"C:\\Program Files\\RenderDoc\\renderdoc.dll\""

			ideConfigGenerated = true
		}
	}
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	// Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
	it.options.release = 17
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}
